чалавекы : array {
	"Міша"
	"Жора"
}

OnFirstBoot
{
	stroke = 0
	LastTalk = ""
	чалавек = "Міша"
	можнаЗаказваць = 0
	мова = "бел."
	заказаліНапой = 0
	напой = ""
	заказаліЕжа = 0
	ежа = ""
	еліБрудны = 0
	харчаваліІмі = 0
	еліІх = 0
	харчаваліБрудны = 0
	размаўленне = 0
	хвілінаІдзе = 0
	першыЗаказ = 0
	чалавекзакавалі = 0
	масіўЯкары = IARRAY
	
	"\1\s[10]\0\s[0]А, прывітанне! Я вельмі рад, што ты тут. Ёсць шмат аб чым хачу гаворыць сёння. Не трэба размаўляць, калі не хочаш. ...Але, это ж ты казалі, што проста хацелі слухаць......"
}

OnInitialize
{
	//Для новые переменние в обновление, лучше пишите вот так:
	//if !ISVAR("НоваяПеременная"); НоваяПеремення = 0
	//Это значить, что если у пользователя уже есть эту переменную, она не будет сброшено.
	
	if reference0 != "reload" //Для вещи, которые хочешь сбросить ТОЛЬКО если не просто перезагрузишь. Что-то для тебя, разработчик, в основном.
	{
		stroke = 0
		можнаЗаказваць = 0
		заказаліНапой = 0
		напой = ""
		заказаліЕжа = 0
		ежа = ""
		еліБрудны = 0
		харчаваліІмі = 0
		еліІх = 0
		харчаваліБрудны = 0
		размаўленне = 0
		хвілінаІдзе = 0
		першыЗаказ = 0
		чалавекзакавалі = 0
		масіўЯкары = IARRAY
	
		LastTalk = ""
	}
}

OnBoot {
	чалавек = ANY(чалавекы)
	case чалавек {
		when "Жора" {JoraBoot}
		others {MishaBoot} //fallback
	}
}

MishaBoot {
	"Вось і я, твой улюбімы чалавек... Ну я не ведаю. Можа быць ты больш любіш хтось другы.\e"
}

JoraBoot {
	"О, прывітанне! Ты доўга чакалі? Не пережівай, потому што я здесь цяперь! А ці гэта не добра? Я лічу што гэта добра.\e"
}

OnClose
{
	можнаЗаказваць = 0
	заказаліНапой = 0
	напой = ""
	заказаліЕжа = 0
	ежа = ""
	размаўленне = 0
	хвілінаІдзе = 0
	першыЗаказ = 0
	чалавекзакавалі = 0
	case чалавек {
		when "Жора" {JoraClose}
		others {MishaClose}
	}
	--
	"\x\-" //Это то, что закрывает привидении (после короткую паузу). Не удаляйте \-, и БУДЬ УВЕРЕН что перед этой точкой не используешь \е - в тое случае это не работает!
}

MishaClose {
	"Да пабачэння."
}

JoraClose {
	"Пока-пока! Надеюсь что мы скора пабачімся!"
}