OnSakuraMenu : all {
	"\0\s[0]\b2\![set,autoscroll,disable]"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings //Это проверяние здесь, чтобы они не будет говорить, при нажатии кнопку 'назад'. удалить, если хочешь.
	
	"\_q\n[half]"
	if можнаЗаказваць == 1; "\__q[OnOrderMenu]==-=Заказвай=-==\__q"
	" \__q[OnToggleMova]-=(мова меню: ==%(мова)==)=-\__q\n\n"
	"%*\q[Размаўляй,OnAiTalk]"
	if LastTalk != ""; "  %*\q[Кажыце гэта зноў?,OnLastTalk]"
	"\n"
	"%*\__q[OnLikAckou]Лік ачкоў\__q"
	if масіўЯкары; " %*\__q[OnJakary]Цікавынкі\__q"
	"\n" //to display correctly
	"%*\__q[OnNameTeach]Нік: %(нік)\__q\n" //other.dic
	"%*\q[Час размаўленне,OnTalkRateMenu]\n" //other.dic
	
	"\n[half]%*\q[...Гэта ўсё, пустой]\n\n[half]"
	"Ежа: %(ежа) Напой: %(напой) | %(размаўленне) | %(хвілінаІдзе)\_q"
}

OnLastTalk {
	LastTalk
}

OnToggleMova {
	if мова == "бел." {
		мова = "Рус."
	}
	elseif мова == "Рус." {
		мова = "Англ."
	}
	else {
		мова = "бел."
	}
	OnSakuraMenu
}

OnLikAckou : all {
	"\_q"
	"Ты...\n\n[half]"
	"елі брудну ежу %(еліБрудны) раз\n"
	"елі ежа у іншыя чалавекі %(еліІх) раз\n"
	"харчавалі імі %(харчаваліІмі) раз\n"
	"харчавалі імі брудну ежу %(харчаваліБрудны) раз\n"
	"\n\n[half]%*\q[Вярнуць,OnSakuraMenu]  %*\q[...Гэта ўсё,пустой]\_q"
}

OnJakary : all {
	LOGGING(reference)
	"\_q"
	if reference0 == "" {
		_агляд = ""
		foreach масіўЯкары; _якар {
			_агляд += "%*\__q[OnJakary,%(_якар)]%(_якар)\__q "
		}
		"%(_агляд)"
		"\n\n[half]%*\q[Вярнуць,OnSakuraMenu]  %*\q[...Гэта ўсё,пустой]\_q"
	}
	else {
		case reference0 {
			when "Міра" {
				"Бывшая жена у Мішы. Здаецца, штосці между імі прошло не так, але яны усё яшчэ добрыя сябры."
			}
			when "Яся" {
				"Здаецца, Пашінай сяброўку. Але калі яны зараз не разам..."
			}
			when "Паша" {
				"Ціхы, сумлівы чалавек. Яго сябры дастаткова добра ему падабаюцца."
			}
			when "Ваня" {
				"Ён дрэнна грае у шахы. Міша не размаўляе пра яму, чамусці."
			}
			when "Вадік" {
				"Любіць кошкы. Луч сонца у цемры. Удзелнічае у іншы гурт з ЯкЖора і Ліна."
			}
			when "Жора" {
				"Мілы, але яму трэба размаўляць пра сябе проста *крыху* меньше. Удзелнічае у іншы гурт з свою жену, Ліну, а ЯкВадік."
			}
			when "Міша" {
				"Женаты і мае малую дачку. Як-то здарыцца, што шмат размаўляе пра Пашу. Занадта шмат, можаце казаць."
			}
			when "Зіна" {
				"Сучасная жена Мішы. Любіць малавядомыя фільмы. Думае, што свой муж занадта шмат працуе. Яе не падабаецца Паша."
			}
			others {}
		}
		"\_q\x\![raise,OnJakary]"
	}
}

OnOrderMenu : all {
	if ежа != "" && напой != "" || заказаліНапой == 1 && заказаліЕжа == 1; можнаЗаказваць = 0
	_агляд = ""
	_другыАгляд = ""
	"\b[2]\_q==-Напоі-==\n\n[half]"
	foreach масіўНапоі; _напой {
		if заказаліНапой == 1 {
			_агляд += "%*\f[color,disabled]%(_напой) "
		}
		else {
			_агляд += "%*\__q[OnOrder,напой,%(_напой)]%(_напой)\__q "
		}
	}
	"%(_агляд)\f[color,default]"
	"\n\n[half]==-Ежа-==\n\n[half]"
	foreach масіўЕжа; _ежа {
		if заказаліЕжа == 1 {
			_другыАгляд += "%*\f[color,disabled]%(_ежа) "
		}
		else {
			_другыАгляд += "%*\__q[OnOrder,ежа,%(_ежа)]%(_ежа)\__q "
		}
	}
	"%(_другыАгляд)\f[color,default]"
	"\n\n%*\q[Вярнуць,OnSakuraMenu]  %*\q[...Гэта ўсё,пустой]\_q"
}

OnOrder {
	if першыЗаказ == 0; першыЗаказ = 1
	if reference0 == "напой" {
		заказаліНапой = 1
		напой = "%(reference1)"
	}
	else {
		заказаліЕжа = 1
		ежа = "%(reference1)"
	}
	OnOrderMenu
}

масіўЕжа : array {
	case мова {
		when "Англ." {
			"Cinnamon Bun"
			"Bagel"
			"Cheesecake"
			"Donut"
			"Macarons"
			"Chocolates"
		}
		when "Рус." {
			"Улитка с Корицей"
			"Бейгл"
			"Чизкейк"
			"Пончик"
			"Макарон"
			"Шоколад"
		}
		others { //default to by
			"Булачка з Карыцей"
			"Бэйгл"
			"Сырны пірог"
			"Пончык"
			"Макарон"
			"Шакалад"
		}
	}
}

масіўНапоі : array {
	case мова {
		when "Англ." {
			"Coffee"
			"Tea"
			"Smoothie"
		}
		when "Рус." {
			"Кофе"
			"Чай"
			"Смузи"
		}
		others { //default to by
			"Кава"
			"Гарбата"
			"Смузі"
		}
	}
}

OnCalaviekZakaz {
	if reference0 == "заказ" {
		if reference2 != "" {
			заказаліНапой = 1
			напой = "%(reference2)"
		}
		if reference1 != "" {
			заказаліЕжа = 1
			ежа = "%(reference1)"
		}
	}
	else {
		чалавекзаказвалі = 1
		_ежа = ""
		_напой = ""
		case чалавек {
			when "Міша" {
				"...Ну, калі ты не хочаш есці, я буду атрымаць для сябе %(_ежа = масіўЕжа[0]) і %(_напой = масіўНапоі[0])."
			}
			when "Жора" {
				"Нічога не хочаш? Ну, я хачу %(_ежа = масіўЕжа[3]). А буду есці ему сам!"
			}
			others {}
		}
		--; "\x\![raise,OnCalaviekZakaz,заказ,%(_ежа),%(_напой)]"
	}
}

OnCalaviekJesci {
	if reference0 == "заказ" {
		if reference2 != "" {
			заказаліНапой = 1
			напой = "%(reference2)"
		}
		if reference1 != "" {
			заказаліЕжа = 1
			ежа = "%(reference1)"
		}
	}
	else {
		if чалавекзаказвалі == 1 {
			case чалавек {
				when "Міша" {
					"...Ой, я так шмат размаўляў, што забыў пра ежу. Ну, лепше есці яму зараз, так?"
				}
				when "Жора" {
					"А... ежу яшчэ там...... ну, гэта же мая."
				}
				others {}
			}
			--; "\x"
		}
		else {
			case чалавек {
				when "Міша" {
					"...Тры часа, а ты не елі свой %(ежа)"
					--
					if напой; ", не пілі свой %(напой)..."
					--
					" Так што, мне здаеццв, што гэта моё цяпер. Спадзяюся, ты так і не хацелі..."
				}
				when "Жора" {
					"Ну... я галодны... а ты не елі... мабыць хацелі, што я з'еў гэта? Так і буду рабіць!"
				}
				others {}
			}
			--; "\x"
		}
		ежа = ""; напой = ""
	}
}